apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'com.novoda.bintray-release'

buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath deps.build.gradlePlugins.kotlin
        classpath deps.build.gradlePlugins.bintray
    }
}

android {
    compileSdkVersion deps.build.compileSdkVersion
    buildToolsVersion deps.build.buildToolsVersion

    defaultConfig {
        minSdkVersion deps.build.minSdkVersion
        targetSdkVersion deps.build.targetSdkVersion
        versionCode 1
        versionName "1.0.0"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    lintOptions {
        abortOnError false
    }
}

dependencies {
    api deps.kotlin.stdlib
    implementation deps.support.appcompat
}

task deleteJar(type: Delete) {
    delete 'libs/jars/simpleauth-common.jar'
}

task createJar(type: Copy) {
    from('build/intermediates/bundles/release/')
    into('libs/jars/')
    include('classes.jar')
    rename('classes.jar', 'simpleauth-common.jar')
}

tasks.withType(Javadoc).all {
    excludes = ['**/*.kt']

    enabled = false
    options.addBooleanOption('Xdoclint:none', true)
}

createJar.dependsOn(deleteJar, build)

publish {
    userOrg = 'jaychang0917'
    groupId = 'com.jaychang'
    artifactId = 'simpleauth'
    publishVersion = '2.1.1'
    desc = 'A easy to use social authentication android library. (Facebook, Google, Twitter, Instagram)'
    website = 'https://github.com/jaychang0917/SimpleAuth'
}
